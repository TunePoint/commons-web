plugins {
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'java'
}

allprojects {
    group = 'ua.tunepoint'
    version = '0.0.2'
    sourceCompatibility = '17'
}

subprojects {

    apply plugin: 'maven-publish'
    apply plugin: "io.spring.dependency-management"
    apply plugin: 'java-library'

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    repositories {
        mavenCentral()
    }

    dependencyManagement {
        dependencies {

            dependency 'org.springframework:spring-web:5.3.18'
            dependency 'org.projectlombok:lombok:1.18.22'
            dependency 'org.slf4j:slf4j-api:1.7.36'

        }
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                groupId project.group
                artifactId project.name
                version project.version

                artifact jar
            }
        }

        repositories {
            maven {
                name  'reposilite'
                url "http://${System.getenv('ARTIFACTORY_HOST')}/${System.getenv('ARTIFACTORY_REPOSITORY')}"
                allowInsecureProtocol true

                credentials {
                    username System.getenv('ARTIFACTORY_USERNAME')
                    password System.getenv('ARTIFACTORY_PASSWORD')
                }
            }
        }
    }
}